apply plugin: 'java'
apply plugin: 'checkstyle'
apply plugin: "com.github.spotbugs"

apply from: "$rootDir/gradle/repositories.gradle"

dependencies {
    checkstyle project(':build-config:checkstyle')
    checkstyle 'com.puppycrawl.tools:checkstyle:7.8.2'

    implementation 'org.slf4j:slf4j-api:1.7.22'
}

sourceCompatibility = '1.8'

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

checkstyle {
    toolVersion '7.8.2'
    configFile rootProject.file("build-config/checkstyle/build/checkstyle.xml")
}

// startsWith had to be used https://github.com/spotbugs/spotbugs-gradle-plugin/issues/32
tasks.findAll { it.name.startsWith('spotbugs') }.each {
    it.effort "max"
    it.reportLevel "low"
    it.reports {
        xml.enabled false
        html.enabled true
    }
    def excludeFile = file('spotbugs-exclude.xml')
    if(excludeFile.exists()) {
        it.excludeFilter = excludeFile
    }
}

